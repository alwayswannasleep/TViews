apply plugin: 'com.android.library'

def projectVersion = "1.0.0"
def gitUrl = 'https://github.com/alwayswannasleep/TViews.git'
def siteUrl = 'https://github.com/alwayswannasleep/TViews'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName projectVersion
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFile 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.android.support:design:24.2.1'
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'TViews'
            packaging 'aar'
            url siteUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'vkraevskiy'
                    name 'Vlad Kraevskiy'
                    email 'vladkraevskiy@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    publications = ['Publication']

    pkg {
        repo = 'TViews'
        licenses = ['Apache-2.0']
        name = project.name
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true

        version {
            name = projectVersion
            vcsTag = projectVersion
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/tviews-release.aar")
            groupId 'com.vkraevskiy.tviews'
            artifactId 'tviews'
            version projectVersion
        }
    }
}
